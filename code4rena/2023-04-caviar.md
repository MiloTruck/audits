# Caviar Private Pools
The code under review can be found in [2023-04-caviar](https://github.com/code-423n4/2023-04-caviar).

## Findings Summary
| ID | Description | Severity |
| - | - | - |
| [M-01](#m-01-royalty-fee-calculations-are-non-compliant-with-eip-2981) | Royalty fee calculations are non-compliant with EIP-2981 | Medium |
| [L-01](#l-01-nfts-might-become-impossible-to-buy-in-private-pools) | NFTs might become impossible to buy in private pools | Low |

## [M-01] Royalty fee calculations are non-compliant with EIP-2981

[EIP-2981](https://eips.ethereum.org/EIPS/eip-2981), the NFT Royalty Standard, states:
> The implementer **MAY** choose to change the percentage value based on other predictable variables that do not make assumptions about the unit of exchange.
>
> One more reasonable approach **MAY** use the number of transfers of an NFT to decide which percentage value is used to calculate the `royaltyAmount`. The idea being that the percentage value could decrease after each transfer of the NFT.
>
> Another example could be using a different percentage value for each unique `_tokenId`.

As seen from above, contracts that implement EIP-2981 might return different royalty fees when the `royaltyInfo()` function is called:
1. NFTs with different token IDs might have different royalty fees.
2. Royalty fees might change based on the current number of transfers of an NFT.

However, the way this protocol implements the calculation of royalty fees does not account for the above, making it non-compliant with EIP-2981.

### 1. NFTs with different token IDs might have different royalty fees.

#### Vulnerability Details

In the `buy()` function of the `PrivatePool` contract, the royalty fee amount is calculated as such:

[PrivatePool.sol#L235-L249](https://github.com/code-423n4/2023-04-caviar/blob/main/src/PrivatePool.sol#L235-L249)
```solidity
// calculate the sale price (assume it's the same for each NFT even if weights differ)
uint256 salePrice = (netInputAmount - feeAmount - protocolFeeAmount) / tokenIds.length;
uint256 royaltyFeeAmount = 0;
for (uint256 i = 0; i < tokenIds.length; i++) {
    // transfer the NFT to the caller
    ERC721(nft).safeTransferFrom(address(this), msg.sender, tokenIds[i]);

    if (payRoyalties) {
        // get the royalty fee for the NFT
        (uint256 royaltyFee,) = _getRoyalty(tokenIds[i], salePrice);

        // add the royalty fee to the total royalty fee amount
        royaltyFeeAmount += royaltyFee;
    }
}
```

Where:
* `tokenIds` - Array containing the token IDs of each NFT being bought.
* `netInputAmount` - The total price of all NFTs.

In the code above, `salePrice` is taken as the average price of each NFT to be bought. Afterwards, the `royaltyFee` for each NFT transfer is calculated based on `salePrice`, instead of the actual price of each NFT.

However, this implementation would return an incorrect `royaltyFeeAmount` if:
1. The royalty fee implementation has different royalty fees for NFTs with different token IDs,
2. `buy()` is called with multiple NFTs that have different prices.

By calling `_getRoyalty()` with the average sale price of each NFT instead of its actual price, the `royaltyFee` returned for that NFT would be incorrect.

#### Proof of Concept

Consider a royalty fee implementation that has double the royalty fee for an NFT where `tokenId == 100`:
```solidity
function royaltyInfo(uint256 tokenId, uint256 salePrice) public view override returns (address receiver, uint256 royaltyAmount) {
    receiver = address(0xbeefbeef);

    royaltyAmount = salePrice * royaltyFeeRate / 1e18;
    if (tokenId == 100) {
        royaltyAmount = 2 * salePrice * royaltyFeeRate / 1e18;
    }
}
```

If a user calls `buy()` with the following NFTs:
- `tokenId = 100, price = 2e18`
- `tokenId = 1, price = 1e18`

the average sale price, would be `15e17`.

If each transfer has a 1% royalty fee (`royaltyFeeRate = 1e16`), the total royalty fee amount should be:
```
royaltyFeeAmount = (2 * 2e18 * 1e16 / 1e18) + (1e18 * 1e16 / 1e18) = 5e16
```

However, as `buy()` uses the average sale price to calculate royalty fees for each transfer, the total royalty fee amount becomes:
```
royaltyFeeAmount = (2 * 15e17 * 1e16 / 1e18) + (15e17 * 1e16 / 1e18) = 4.5e16
```

Thus, the royalty recipient receives less royalty fees than he is supposed to.

#### Recommended Mitigation

Consider calling `_getRoyalty()` with the actual price of each NFT being transferred, instead of the average price.

Note that this vulnerability is also present in the following functions:
- `sell()` in `PrivatePool.sol`.
- `buy()` in `EthRouter.sol`.
- `sell()` in `EthRouter.sol`.


### 2. Royalty fees might change based on the current number of transfers of an NFT.

#### Vulnerability Details

In the `buy()` function, `_getRoyalty()` is first called in a loop:

[PrivatePool.sol#L235-L249](https://github.com/code-423n4/2023-04-caviar/blob/main/src/PrivatePool.sol#L235-L249)
```solidity
// calculate the sale price (assume it's the same for each NFT even if weights differ)
uint256 salePrice = (netInputAmount - feeAmount - protocolFeeAmount) / tokenIds.length;
uint256 royaltyFeeAmount = 0;
for (uint256 i = 0; i < tokenIds.length; i++) {
    // transfer the NFT to the caller
    ERC721(nft).safeTransferFrom(address(this), msg.sender, tokenIds[i]);

    if (payRoyalties) {
        // get the royalty fee for the NFT
        (uint256 royaltyFee,) = _getRoyalty(tokenIds[i], salePrice);

        // add the royalty fee to the total royalty fee amount
        royaltyFeeAmount += royaltyFee;
    }
}
```

As seen above, `_getRoyalty()` is called in a loop that transfers each NFT.

Later on, `_getRoyalty()` is called again when transferring royalty fees to the recipient:

[PrivatePool.sol#L273-L283](https://github.com/code-423n4/2023-04-caviar/blob/main/src/PrivatePool.sol#L273-L283)
```solidity
// get the royalty fee for the NFT
(uint256 royaltyFee, address recipient) = _getRoyalty(tokenIds[i], salePrice);

// transfer the royalty fee to the recipient if it's greater than 0
if (royaltyFee > 0 && recipient != address(0)) {
    if (baseToken != address(0)) {
        ERC20(baseToken).safeTransfer(recipient, royaltyFee);
    } else {
        recipient.safeTransferETH(royaltyFee);
    }
}
```

However, the second call to `_getRoyalty()` might return a different `royaltyFee` for a royalty fee implementation that calculates the royalty fee based on the current number of transfers of an NFT. This would result in either:
1. The `buy()` function reverting due to an insufficient amount of `baseToken`.
2. The royalty recipient receiving less royalty fees than intended.

#### Proof of Concept

Consider a royalty fee implementation that increases royalty fees based on the total number of transfers of the NFT collection:
```solidity
function royaltyInfo(uint256 tokenId, uint256 salePrice) public view override returns (address receiver, uint256 royaltyAmount) {
    receiver = address(0xbeefbeef);

    royaltyAmount = salePrice * royaltyFeeRate * totalTransferCount / 1e18;
}
```

Where:
- `totalTransferCount` - The total number of transfers of the NFT collection.

In the `buy()` function, when  `_getRoyalty()` is called for the second time, `totalTransferCount` will be larger than the first call `_getRoyalty()`, resulting in a larger `royaltyFee` being returned.

#### Recommended Mitigation

Consider storing the royalty fee and recipient for each NFT from the first call to `_getRoyalty()`, and using these stored values when transferring `baseToken` to the recipient later on.

## [L-01] NFTs might become impossible to buy in private pools

### Vulnerability Details

In the `PrivatePool` contract, the `buyQuote()` function is used to determine the price of NFTs being purchased based on their total weight:

[PrivatePool.sol#L689-L706](https://github.com/code-423n4/2023-04-caviar/blob/main/src/PrivatePool.sol#L689-L706)
```solidity
/// @notice Returns the required input of buying a given amount of NFTs inclusive of the fee which is dependent on
/// the currently set fee rate.
/// @param outputAmount The amount of NFTs to buy multiplied by 1e18.
/// @return netInputAmount The required input amount of base tokens inclusive of the fee.
/// @return feeAmount The fee amount.
function buyQuote(uint256 outputAmount)
    public
    view
    returns (uint256 netInputAmount, uint256 feeAmount, uint256 protocolFeeAmount)
{
    // calculate the input amount based on xy=k invariant and round up by 1 wei
    uint256 inputAmount =
        FixedPointMathLib.mulDivUp(outputAmount, virtualBaseTokenReserves, (virtualNftReserves - outputAmount));

    protocolFeeAmount = inputAmount * Factory(factory).protocolFeeRate() / 10_000;
    feeAmount = inputAmount * feeRate / 10_000;
    netInputAmount = inputAmount + feeAmount + protocolFeeAmount;
}
```

Where:
- `outputAmount` - Total weight of NFTs being bought.
- `virtualNftReserves` - Total weight of NFTs in the private pool.
- `virtualBaseTokenReserves` - Total amount of base tokens in the private pool.

As seen from above, the function divides `outputAmount * virtualBaseTokenReserves` by `virtualNftReserves - outputAmount`. As such, if `outputAmount` is greater than or equal to `virtualNftReserves`, `buyQuote()` will revert due to either a division by 0, or an arithmetic underflow.

In the `buy()` function, `buyQuote()` is used to determine the price of NFTs being bought:

[PrivatePool.sol#L221-L231](https://github.com/code-423n4/2023-04-caviar/blob/main/src/PrivatePool.sol#L221-L231)
```solidity
// calculate the required net input amount and fee amount
(netInputAmount, feeAmount, protocolFeeAmount) = buyQuote(weightSum);

// Some code here...

// update the virtual reserves
virtualBaseTokenReserves += uint128(netInputAmount - feeAmount - protocolFeeAmount);
virtualNftReserves -= uint128(weightSum);
```

Where:
- `weightSum` - Total weight of NFTs being bought.

Additionally, whenever an NFT is bought, `virtualNftReserves` is reduced by its weight. As such, if `virtualNftReserves` is set to exactly the total weight of all NFTs in the private pool when it is initialized, `buy()` fails when there is only one last NFT remaining in the private pool, as `virtualNftReserves` equals to its weight, causing `buyQuote()` to revert.

Furthermore, an attacker can manipulate `virtualNftReserves` to become smaller by directly transferring NFTs to the private pool, and then purchasing them using the `buy()` function. This could potentially cause more NFTs to become stuck in the pool if `virtualNftReserves` becomes sufficiently small.

### Proof of Concept

The code below contains two tests:
- `test_BuyingLastNftWillRevert()` demonstrates how the last NFT remaining will be stuck in the pool.
- `test_AttackerCanReduceVirtualNftReserves()` demonstrates how an attacker can cause `virtualNftReserves` to become smaller, causing `buy()` to be temporarily DOSed.

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.19;

import "./Fixture.sol";
import "../src/PrivatePool.sol";

contract PrivatePool_POC is Fixture {
    PrivatePool privatePool;

    uint256[] tokenIds;
    uint256[] tokenWeights;
    PrivatePool.MerkleMultiProof proofs;

    function setUp() public {
        // Deploy and initialize private pool
        privatePool = new PrivatePool(
            address(factory),
            address(royaltyRegistry),
            address(stolenNftOracle)
        );
        privatePool.initialize(
            address(0), // baseToken
            address(milady), // nft
            100 ether, // virtualBaseTokenReserves
            5e18, // virtualNftReserves
            0, // changeFee
            0, // feeRate
            bytes32(0), // merkleRoot
            false, // useStolenNftOracle
            false // payRoyalties
        );

        // Pool starts with 5 miladys
        for (uint256 i = 0; i < 5; i++) {
            milady.mint(address(privatePool), i);
        }
    }

    function test_BuyingLastNftWillRevert() public {
        // Buy the first 4 nfts
        tokenIds.push(0);
        tokenIds.push(1);
        tokenIds.push(2);
        tokenIds.push(3);
        privatePool.buy{value: 400 ether}(tokenIds, tokenWeights, proofs);

        // Buying the last nft will revert
        delete tokenIds;
        tokenIds.push(4);
        vm.expectRevert();
        privatePool.buy{value: 100 ether}(tokenIds, tokenWeights, proofs);
    }

    function test_AttackerCanReduceVirtualNftReserves() public {
        // Repeatedly buy and transfer nft 0
        tokenIds.push(0);
        while (privatePool.virtualNftReserves() > 1e18) {
            (uint256 netInputAmount, , ) = privatePool.buyQuote(1e18);
            privatePool.buy{value: netInputAmount}(
                tokenIds,
                tokenWeights,
                proofs
            );

            milady.transferFrom(address(this), address(privatePool), 0);
        }

        // virtualNftReserves becomes 1e18
        assertEq(privatePool.virtualNftReserves(), 1e18);

        // All other nfts cannot be bought
        delete tokenIds;
        tokenIds.push(4);
        vm.expectRevert();
        privatePool.buy{value: 100 ether}(tokenIds, tokenWeights, proofs);
    }
}
```

### Recommended Mitigation

To prevent the last NFT remaining in the pool from becoming stuck, ensure that `virtualNftReserves` is set to a value larger than the total weight of all NFTs in the pool during initialization.