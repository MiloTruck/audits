# Teller V2
The code under review can be found in [teller-protocol-v2](https://github.com/teller-protocol/teller-protocol-v2/tree/cb66c9e348cdf1fd6d9b0416a49d663f5b6a693c).

## Findings Summary

| ID | Description | Severity |
| - | - | - |
| [H-01](#h-01-anyone-can-commit-collateral-on-behalf-of-borrowers-for-pending-bids) | Anyone can commit collateral on behalf of borrowers for pending bids | High |
| [M-01](#m-01-changing-lendermanager-to-a-new-address-will-break-getloanlender) | Changing `lenderManager` to a new address will break `getLoanLender()` |  Medium |
| [M-02](#m-02-market-owners-can-manipulate-marketplace-fee-to-steal-principal-from-borrowers) | Market owners can manipulate marketplace fee to steal principal from borrowers | Medium |
| [M-03](#m-03-protocol-does-not-support-fee-on-transfer-tokens) | Protocol does not support fee-on-transfer tokens | Medium |
| [M-04](#m-04-lenders-can-steal-collateral-from-liquidators-if-no-revert-on-failure-tokens-are-used) | Lenders can steal collateral from liquidators if no-revert-on-failure tokens are used | Medium |
| [M-05](#m-05-anyone-can-claim-defaulted-loans-collateral-on-behalf-of-lender) | Anyone can claim defaulted loan's collateral on behalf of lender | Medium |
| [L-01](#l-01-borrowers-reputation-mark-becomes-permanent-if-his-loan-is-defaulted-and-claimed-by-lender) | Borrower's reputation mark becomes permanent if his loan is defaulted and claimed by lender | Low |
| [L-02](#l-02-incorrect-code-in-_withdrawcollateral) | Incorrect code in `_withdrawCollateral()` | Low |
| [L-03](#l-03-protocol-functionality-will-break-after-7-february-2106) | Protocol functionality will break after 7 February 2106 | Low |

## [H-01] Anyone can commit collateral on behalf of borrowers for pending bids

### Summary

Due to a lack of caller validation, a lender can steal collateral from borrowers by committing collateral on his behalf.

### Vulnerability Detail

In `CollateralManager.sol`, the `commitCollateral()` function is used to commit collateral after a user has submitted a bid:

[CollateralManager.sol#L111-L130](https://github.com/teller-protocol/teller-protocol-v2/blob/cb66c9e348cdf1fd6d9b0416a49d663f5b6a693c/packages/contracts/contracts/CollateralManager.sol#L111-L130)

```solidity
    /**
     * @notice Checks the validity of a borrower's multiple collateral balances and commits it to a bid.
     * @param _bidId The id of the associated bid.
     * @param _collateralInfo Additional information about the collateral assets.
     * @return validation_ Boolean indicating if the collateral balances were validated.
     */
    function commitCollateral(
            uint256 _bidId,
        Collateral[] calldata _collateralInfo
    ) public returns (bool validation_) {
            address borrower = tellerV2.getLoanBorrower(_bidId);
        (validation_, ) = checkBalances(borrower, _collateralInfo);

        if (validation_) {
                for (uint256 i; i < _collateralInfo.length; i++) {
                    Collateral memory info = _collateralInfo[i];
                _commitCollateral(_bidId, info);
            }
        }
    }
```

However, as the function does not ensure that the caller is the borrower of the bid, anyone can call `commitCollateral()` to change the borrower's committed collateral.

If the borrower has any existing approvals for the `CollateralManager` contract, an attacker can abuse `commitCollateral()` to steal all of his approved collateral. 

### Impact

If a borrower has a pending bid and has approved the `CollateralManager` contract to transfer any of his assets, an attacker can steal those assets by committing them as collateral for the pending bid, and then accepting the bid.

Note that the likelihood of a borrower having existing approvals for the `CollateralManager` contract is quite high as:
- Borrowers must approve the `CollateralManager` contract to transfer their committed collateral for lenders to accept their bids.
- For ERC-1155, the only way to approve the `CollateralManager` contract is to use `setApprovalForAll()`, which allows the contract to transfer **ALL** their ERC-1155 tokens in the account.

### Proof of Concept

Consider the following scenario:
- Bob submits two bids with collateral through `TellerV2.submitBid()`:
  - Bid A has 10 tokens as collateral.
  - Bid B has 20 tokens as collateral.
- To allow lenders to accept his bids, he approves the `CollateralManager` contract to transfer 30 of his tokens.
- Alice, a malicious lender, calls `commitCollateral()` with bid A's ID, but specifies 30 tokens as collateral.
  - Due to the lack of caller validation, this call works.
- Alice accepts bid A using `TellerV2.lenderAcceptBid()`.

In the scenario above, Alice steals 20 tokens from Bob by forcing him to deposit 30 tokens as collateral for bid A. If Bob does not repay bid A in time, Alice will gain an additional 20 tokens.

### Recommendation

Ensure that only the borrower can call `commitCollateral()` for his bid:

[CollateralManager.sol#L117-L130](https://github.com/teller-protocol/teller-protocol-v2/blob/cb66c9e348cdf1fd6d9b0416a49d663f5b6a693c/packages/contracts/contracts/CollateralManager.sol#L117-L130)

```diff
    function commitCollateral(
            uint256 _bidId,
        Collateral[] calldata _collateralInfo
    ) public returns (bool validation_) {
            address borrower = tellerV2.getLoanBorrower(_bidId);
+       require(_msgSender() == borrower, \"Not borrower');        
        (validation_, ) = checkBalances(borrower, _collateralInfo);

        if (validation_) {
                for (uint256 i; i < _collateralInfo.length; i++) {
                    Collateral memory info = _collateralInfo[i];
                _commitCollateral(_bidId, info);
            }
        }
    }
```

[CollateralManager.sol#L138-L147](https://github.com/teller-protocol/teller-protocol-v2/blob/cb66c9e348cdf1fd6d9b0416a49d663f5b6a693c/packages/contracts/contracts/CollateralManager.sol#L138-L147)

```diff
    function commitCollateral(
            uint256 _bidId,
        Collateral calldata _collateralInfo
    ) public returns (bool validation_) {
            address borrower = tellerV2.getLoanBorrower(_bidId);
+       require(_msgSender() == borrower, \"Not borrower');        
        validation_ = _checkBalance(borrower, _collateralInfo);
        if (validation_) {
                _commitCollateral(_bidId, _collateralInfo);
        }
    }
```

## [M-01] Changing `lenderManager` to a new address will break `getLoanLender()`

### Summary

Changing the `lenderManager` of the `TellerV2` contract might result in the `getLoanLender()` function returning an incorrect address for certain loans, causing lenders to never get their loan's repayments. 

### Vulnerability Detail

In `TellerV2.sol`, lenders can use the `claimLoanNFT()` function to get an NFT that represents ownership of a loan:

[TellerV2.sol#L560-L574](https://github.com/teller-protocol/teller-protocol-v2/blob/cb66c9e348cdf1fd6d9b0416a49d663f5b6a693c/packages/contracts/contracts/TellerV2.sol#L560-L574)

```solidity
    function claimLoanNFT(uint256 _bidId)
        external
        acceptedLoan(_bidId, \"claimLoanNFT\")
        whenNotPaused
    {
            // Retrieve bid
        Bid storage bid = bids[_bidId];

        address sender = _msgSenderForMarket(bid.marketplaceId);
        require(sender == bid.lender, \"only lender can claim NFT\");
        // mint an NFT with the lender manager
        lenderManager.registerLoan(_bidId, sender);
        // set lender address to the lender manager so we know to check the owner of the NFT for the true lender
        bid.lender = address(lenderManager);
    }
```

This sets the bid's lender to the `lenderManager` address. The bid's actual lender is then determined by the current owner of NFT, as seen in the `getLoanLender()` function:

[TellerV2.sol#L1032-L1047](https://github.com/teller-protocol/teller-protocol-v2/blob/cb66c9e348cdf1fd6d9b0416a49d663f5b6a693c/packages/contracts/contracts/TellerV2.sol#L1032-L1047)

```solidity
    /**
     * @notice Returns the lender address for a given bid. If the stored lender address is the `LenderManager` NFT address, return the `ownerOf` for the bid ID.
     * @param _bidId The id of the bid/loan to get the lender for.
     * @return lender_ The address of the lender associated with the bid.
     */
    function getLoanLender(uint256 _bidId)
        public
        view
        returns (address lender_)
    {
            lender_ = bids[_bidId].lender;

        if (lender_ == address(lenderManager)) {
                return lenderManager.ownerOf(_bidId);
        }
    }
```

However, the owner of the `TellerV2` contract can change the `lenderManager` address once through the `setLenderManager()` function. If this occurs, the `lender_ == address(lenderManager)` check above will fail, causing `getLoanLender()` to return the old `lenderManager` address as the bid's lender instead of the actual lender.

In the `_repayLoan()` function, `getLoanLender()` is used to determine which address to send loan repayments to. As such, if `lenderManager` is ever changed, loan repayments will be sent to the old `lenderManager` address, instead of the actual lender. These assets will forever be stuck in the old `LenderManager` contract.

### Impact

If the `lenderManager` of the `TellerV2` contract is ever changed, loan repayments might be transferred and permanently locked in the old `LenderManager` contract. Lenders of these loans will never get repaid, causing a loss of assets.


### Recommendation

In the `getLoanLender()` function, consider checking if the bid's lender is set to the old `lenderManager` address, and returning the NFT's owner as the lender if so.


## [M-02] Market owners can manipulate marketplace fee to steal principal from borrowers

### Summary

As the marketplace fee is not checked when lenders accept bids from borrowers, market owners can simply set the marketplace fee to 100% to steal all principal from borrowers.

### Vulnerability Detail

In `TellerV2.sol`, borrowers first submit bids for loans using the `submitBid()` functions. Lenders then call the `lenderAcceptBid()` function to accept their bid. Fees are handled in `lenderAcceptBid()` as follows:

[TellerV2.sol#L512-L540](https://github.com/teller-protocol/teller-protocol-v2/blob/cb66c9e348cdf1fd6d9b0416a49d663f5b6a693c/packages/contracts/contracts/TellerV2.sol#L512-L540)

```solidity
        // Transfer funds to borrower from the lender
        amountToProtocol = bid.loanDetails.principal.percent(protocolFee());
        amountToMarketplace = bid.loanDetails.principal.percent(
                marketRegistry.getMarketplaceFee(bid.marketplaceId)
        );
        amountToBorrower =
            bid.loanDetails.principal -
            amountToProtocol -
            amountToMarketplace;
        //transfer fee to protocol
        bid.loanDetails.lendingToken.safeTransferFrom(
                sender,
            owner(),
            amountToProtocol
        );

        //transfer fee to marketplace
        bid.loanDetails.lendingToken.safeTransferFrom(
                sender,
            marketRegistry.getMarketFeeRecipient(bid.marketplaceId),
            amountToMarketplace
        );

        //transfer funds to borrower
        bid.loanDetails.lendingToken.safeTransferFrom(
                sender,
            bid.receiver,
            amountToBorrower
        );
```

As seen from above, protocol and marketplace fees are subtracted from the borrower's principal amount. 

In `MarketRegistry.sol`, market owners are able to change the marketplace fee anytime using the `setMarketFeePercent()` function. As such, if a borrower submits a bid with collateral, market owners can steal their assets by doing the following:
- Set the marketplace fee to 100% using `setMarketFeePercent()`.
- Call `lenderAcceptBid()` to accept the borrower's bid. Due to the 100% marketplace fee, all principal will be transferred back to the market owner.
- The borrower now has a collateralized loan but did not receive any principal. He is forced to repay the loan if he wishes to get his collateral back.

### Impact

The protocol team states that market owners **should not** be able to steal assets in any way possible:
> Market owners should NOT be able to race-condition attack borrowers or lenders by changing market settings while bids are being submitted or accepted (while tx are in mempool). Care has been taken to ensure that this is not possible (similar in theory to sandwich attacking but worse as if possible it could cause unexpected and non-consentual interest rate on a loan) and further-auditing of this is welcome.

However, as demonstrated above, market owners can steal either collateral or principal from borrowers if they submit a bid with collateral.

### Recommendation

In the `submitBid()` function, allow borrowers to specify a maximum fee percentage that they are willing to accept. Ensure that the protocol and marketplace fees do not exceed this percentage in the `lenderAcceptBid()` function.

Alternatively, store the current marketplace fee when borrowers submit a bid using `submitBid()`, and use the stored marketplace fee instead of the current one when `lenderAcceptBid()` is called.

## [M-03] Protocol does not support fee-on-transfer tokens

### Summary

In `CollateralManager.sol`, the `_deposit()` function will always revert when transferring fee-on-transfer tokens.

### Vulnerability Detail

In the `_deposit()` function, the following code is used to transfer ERC-20 tokens used as collateral from the borrower into the escrow contract:

[CollateralManager.sol#L325-L341](https://github.com/teller-protocol/teller-protocol-v2/blob/cb66c9e348cdf1fd6d9b0416a49d663f5b6a693c/packages/contracts/contracts/CollateralManager.sol#L325-L341)

```solidity
        // Pull collateral from borrower & deposit into escrow
        if (collateralInfo._collateralType == CollateralType.ERC20) {
                IERC20Upgradeable(collateralInfo._collateralAddress).transferFrom(
                    borrower,
                address(this),
                collateralInfo._amount
            );
            IERC20Upgradeable(collateralInfo._collateralAddress).approve(
                    escrowAddress,
                collateralInfo._amount
            );
            collateralEscrow.depositAsset(
                    CollateralType.ERC20,
                collateralInfo._collateralAddress,
                collateralInfo._amount,
                0
            );
```

It transfers `collateralInfo._amount` tokens from the borrower, and then attempts to deposit the same amount into the escrow contract using `depositAsset()`. However, if fee-on-transfer tokens are used, the contract will receive less than `collateralInfo._amount` tokens from the borrower, causing the call to `depositAsset()` to revert due to insufficient tokens.

### Impact

The protocol team states that **any** ERC20 token can be used as loan collateral:
> The contracts are expected to comply with the ERC20, ERC721 and ERC1155 EIPs of which any can be used as loan collateral.

However, this is untrue as fee-on-transfer tokens cannot be used as collateral in the protocol, as seen from above.

### Recommendation

Consider stating that fee-on-transfers tokens are not supported as collateral in the documentation. 

## [M-04] Lenders can steal collateral from liquidators if no-revert-on-failure tokens are used

### Summary

Some tokens do not revert on failure, but instead return `false` (e.g. [ZRX](https://etherscan.io/address/0xe41d2489571d322189246dafa5ebde1f4699f498#code)). 

If such tokens are used as collateral, a lender can front-run a liquidator's transaction to steal the defaulted loan's collateral while gaining the loan's repayment.

### Vulnerability Detail

In `CollateralEscrowV1.sol`, the `_withdrawCollateral()` function uses `.transfer()` to transfer ERC-20 tokens:

[CollateralEscrowV1.sol#L164-L170](https://github.com/teller-protocol/teller-protocol-v2/blob/cb66c9e348cdf1fd6d9b0416a49d663f5b6a693c/packages/contracts/contracts/escrow/CollateralEscrowV1.sol#L164-L170)
```solidity
        // Withdraw ERC20
        if (_collateral._collateralType == CollateralType.ERC20) {
                IERC20Upgradeable(_collateralAddress).transfer(
                    _recipient,
                _collateral._amount
            );
        }
```

When `_collateralAddress` is a no-revert-on-failure token, `transfer()` will return `false` instead of reverting if the `CollateralEscrowV1` contract has insufficient collateral. Therefore, even if the contract has no collateral remaining, `_withdrawCollateral()` will not revert.

This creates a scenario where a liquidator can call `TellerV2.liquidateLoanFull()` to liquidate a defaulted loan **after** a lender has already claimed its collateral using `CollateralManager.withdraw()`. The liquidator would still repay the loan's borrowed amount to the lender, but receives no collateral in return.

### Impact

If a no-revert-on-failure token is used as collateral, it is possible for liquidators to liquidate a loan and receive no collateral in return. A malicious lender can also front-run a liquidator's call to `TellerV2.liquidateLoanFull()` to claim the loan's collateral first, stealing it from the liquidator.

### Proof of Concept

Consider the following scenario:
- Alice is a lender of a defaulted loan that uses a no-revert-on-transfer token as collateral.
- Bob calls `TellerV2.liquidateLoanFull()` to liquidate the loan.
- Alice sees Bob's transaction in the mempool and front-runs it, calling `CollateralManager.withdraw()`.
- Alice's transaction executes first, transferring the loan's collateral from the escrow contract to Alice.
- Afterwards, Bob's transaction executes:
  - Bob repays the loan's owed amount to Alice.
  - Although the escrow contract has no collateral left, the transfer in `_withdrawCollateral()` does not revert.
  - The transaction succeeds, but Bob doesn't get any collateral.
 
In the scenario above, Alice steals Bob's collateral by front-running his liquidation transaction.

### Recommendation

In the `_withdrawCollateral()` function, consider performing the transfer with `safeTransfer()` instead:

[CollateralEscrowV1.sol#L164-L170](https://github.com/teller-protocol/teller-protocol-v2/blob/cb66c9e348cdf1fd6d9b0416a49d663f5b6a693c/packages/contracts/contracts/escrow/CollateralEscrowV1.sol#L164-L170)

```diff
        // Withdraw ERC20
        if (_collateral._collateralType == CollateralType.ERC20) {
    -           IERC20Upgradeable(_collateralAddress).transfer(
    +           SafeERC20Upgradeable.safeTransfer(
    +               IERC20Upgradeable(_collateralAddress),
                _recipient,
                _amount
            );
        }
```

## [M-05] Anyone can claim defaulted loan's collateral on behalf of lender

### Summary

 Due to a lack of caller validation, anyone can force a lender to claim their defaulted loan's collateral.

### Vulnerability Detail

In `CollateralManager.sol`, the `withdraw()` function is used to withdraw collateral from a loan that is either repaid or defaulted:

[CollateralManager.sol#L246-L260](https://github.com/teller-protocol/teller-protocol-v2/blob/cb66c9e348cdf1fd6d9b0416a49d663f5b6a693c/packages/contracts/contracts/CollateralManager.sol#L246-L260)

```solidity
    /**
     * @notice Withdraws deposited collateral from the created escrow of a bid that has been successfully repaid.
     * @param _bidId The id of the bid to withdraw collateral for.
     */
    function withdraw(uint256 _bidId) external {
            BidState bidState = tellerV2.getBidState(_bidId);
        if (bidState == BidState.PAID) {
                _withdraw(_bidId, tellerV2.getLoanBorrower(_bidId));
        } else if (tellerV2.isLoanDefaulted(_bidId)) {
                _withdraw(_bidId, tellerV2.getLoanLender(_bidId));
            emit CollateralClaimed(_bidId);
        } else {
                revert(\"collateral cannot be withdrawn\");
        }
    }
```

As seen from above, `withdraw()` does not verify that the caller is either the loan's borrower or lender. As such, whenever a loan becomes defaulted, anyone can call `withdraw()` to withdraw the loan's collateral to the lender. 

### Impact

Only the lender should be able to decide if he wants to claim the defaulted loan's collateral. In certain situations, such as when the collateral's value has decreased significantly, the lender might want the loan to be liquidated instead of claiming its collateral. However, anyone can call `withdraw()` on his behalf, eliminating the possibility of the loan being liquidated.

### Recommendation

Ensure that only the loan's lender can call `withdraw()` to claim collateral when the loan is defaulted:

[CollateralManager.sol#L250-L260](https://github.com/teller-protocol/teller-protocol-v2/blob/cb66c9e348cdf1fd6d9b0416a49d663f5b6a693c/packages/contracts/contracts/CollateralManager.sol#L250-L260)

```diff
    function withdraw(uint256 _bidId) external {
            BidState bidState = tellerV2.getBidState(_bidId);
        if (bidState == BidState.PAID) {
                _withdraw(_bidId, tellerV2.getLoanBorrower(_bidId));
        } else if (tellerV2.isLoanDefaulted(_bidId)) {
    -           _withdraw(_bidId, tellerV2.getLoanLender(_bidId));
+           address lender = tellerV2.getLoanLender(_bidId);
+           require(_msgSender() == lender, \"Only lender can claim\");
+           _withdraw(_bidId, lender);
            emit CollateralClaimed(_bidId);
        } else {
                revert(\"collateral cannot be withdrawn\");
        }
    }
```

## [L-01] Borrower's reputation mark becomes permanent if his loan is defaulted and claimed by lender

### Summary

Borrowers' reputation are not updated if a lender claims their defaulted loan, which will affect their reputation negatively forever.

### Vulnerability Detail

In `TellerV2.sol`, if a loan is repaid or liquidated, the borrower's reputation mark is removed in the `_repayLoan()` function:

[TellerV2.sol#L758-L761](https://github.com/teller-protocol/teller-protocol-v2/blob/cb66c9e348cdf1fd6d9b0416a49d663f5b6a693c/packages/contracts/contracts/TellerV2.sol#L758-L761)

```solidity
        // If the loan is paid in full and has a mark, we should update the current reputation
        if (mark != RepMark.Good) {
                reputationManager.updateAccountReputation(bid.borrower, _bidId);
        }
```

As the `ReputationManager.updateAccountReputation()` function fetches a borrower's active bids using `getBorrowerActiveLoanIds()`, the bid is also removed from `_borrowerBidsActive` to prevent it from ever affecting the borrower's reputation in the future:

[TellerV2.sol#L731-L732](https://github.com/teller-protocol/teller-protocol-v2/blob/cb66c9e348cdf1fd6d9b0416a49d663f5b6a693c/packages/contracts/contracts/TellerV2.sol#L731-L732)

```solidity
            // Remove borrower's active bid
            _borrowerBidsActive[bid.borrower].remove(_bidId);
```

However, if the lender of a defaulted loan claims its collateral through the `CollateralManager.withdraw()` function, the borrower's reputation is not updated, and the bid is never removed from `_borrowerBidsActive`. This will cause the borrower to have a permanent defaulted loan in his reputation whenever someone calls `ReputationManager.getDefaultedLoanIds()`.

### Impact

Borrowers will have a permanent defaulted loan in their reputation for every defaulted loan that is claimed by a lender. This might cause them to be unable to get loans in the future as lenders rely on the reputation system to determine whose bids to accept.

### Recommendation

In the `CollateralManager.withdraw()` function, consider updating the borrower's reputation and removing the bid from `_borrowerBidsActive`. 

## [L-02] Incorrect code in `_withdrawCollateral()`

### Summary

In `CollateralEscrowV1.sol`, the `_withdrawCollateral()` function uses the wrong amount variable when handling ERC-20 transfers.

### Vulnerability Detail

The implementation of the `_withdrawCollateral()` function is as shown:

[CollateralEscrowV1.sol#L151-L170](https://github.com/teller-protocol/teller-protocol-v2/blob/cb66c9e348cdf1fd6d9b0416a49d663f5b6a693c/packages/contracts/contracts/escrow/CollateralEscrowV1.sol#L151-L170)

```solidity
    /**
     * @notice Internal function for transferring collateral assets out of this contract.
     * @param _collateral The collateral asset to withdraw.
     * @param _collateralAddress The address of the collateral contract.
     * @param _amount The amount to withdraw.
     * @param _recipient The address to send the assets to.
     */
    function _withdrawCollateral(
            Collateral memory _collateral,
        address _collateralAddress,
        uint256 _amount,
        address _recipient
    ) internal {
            // Withdraw ERC20
        if (_collateral._collateralType == CollateralType.ERC20) {
                IERC20Upgradeable(_collateralAddress).transfer(
                    _recipient,
                _collateral._amount
            );
        }
```

When calling `.transfer()`, it uses `_collateral.amount` instead of using `_amount`, which is the amount to withdraw.

### Impact

In the current implementation of the protocol, `_collateral.amount` is always equal to `_amount` when `_withdrawCollateral()` is called. As such, this issue is not exploitable. However, this could become exploitable if `CollateralEscrowV1.sol` is ever used in future code.

### Recommendation

Use `_amount` when handling ERC-20 transfers:

[CollateralEscrowV1.sol#L164-L170](https://github.com/teller-protocol/teller-protocol-v2/blob/cb66c9e348cdf1fd6d9b0416a49d663f5b6a693c/packages/contracts/contracts/escrow/CollateralEscrowV1.sol#L164-L170)
```diff
        // Withdraw ERC20
        if (_collateral._collateralType == CollateralType.ERC20) {
                IERC20Upgradeable(_collateralAddress).transfer(
                    _recipient,
-               _collateral._amount
+               _amount
            );
        }
```

## [L-03] Protocol functionality will break after 7 February 2106

### Summary

The protocol casts all time-related values down to `uint32` and then compares/store them. This will cause the protocol's functionality to break in the year 2106. 

### Vulnerability Detail

The protocol downcasts all time-related values to `uint32`, such as `block.timestamp`. For example, in the `_canLiquidateLoan()` function:

[TellerV2.sol#L965-L968](https://github.com/teller-protocol/teller-protocol-v2/blob/cb66c9e348cdf1fd6d9b0416a49d663f5b6a693c/packages/contracts/contracts/TellerV2.sol#L965-L968)

```solidity
        return (uint32(block.timestamp) -
            _liquidationDelay -
            lastRepaidTimestamp(_bidId) >
            bidDefaultDuration[_bidId]);
```

As `type(uint32).max = 4294967295`, `block.timestamp` will be larger than `type(uint32).max` on 7 February 2106. This will cause all time-related comparisons to become incorrect, breaking core functionality of the protocol. For example, `_canLiquidateLoan()` will revert due to an arithmetic underflow after 7 February 2106 if the last repaid timestamp was before that. 

### Impact

After 7 February 2016, core functionality of the protocol will break. Although it is extremely unlikely that the protocol will still be used 83 years later, it is not entirely impossible...

### Recommendation

Consider storing time-related values using `uint40` instead.



